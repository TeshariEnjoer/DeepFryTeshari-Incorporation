/datum/map_template/train_station
	name = "Train Station Template"
	returns_created_atoms = TRUE
	/// Stations replace their block outright rather than stacking onto it - unload_station() resets the turfs to
	/// space anyway, and stacking would both cost a load_on_top() per turf and grow baseturfs on every swap.
	should_place_on_top = FALSE

/datum/train_station
	/// Name of the station
	var/name = "Train station"
	/// Full description of the station
	var/desc = "A generic train station"
	/// Station flags, see the file fenysha_events/__DEFINES/trainstation.dm for details
	var/station_flags = NONE
	/// Whether this station is visible in the train_controller menu; if FALSE - also prevents the station from being selected as the next one
	var/visible = TRUE
	/// How many stations the train needs to visit before this station
	var/required_stations = 0
	/// Maximum number of visits for this station
	var/maximum_visits = 1
	/// How many times this station has been visited
	var/visited = 0
	/// Whether a password is required to unlock this station
	var/required_password = TRUE
	/// Creator of this station, will be displayed when it is visited
	var/creator = "Fenysha"
	/// Threat level at the station
	var/threat_level = THREAT_LEVEL_SAFE
	/// Region in which this station is located
	var/region = "None"
	/// Type of the station
	var/station_type = "unknown"


	var/environment_flags = NONE

	/// Overmap object representing this station
	var/datum/trainmap_object/map_object
	/// Path to the station map, automatically creates a template for it
	var/map_path
	/// list() - ambient sounds that play at this station
	var/list/ambience_sounds = null
	/// List of possible station surroundings (generated above the train)
	var/list/possible_nearstations = list(
		/datum/train_station/near_station/static_default,
		/datum/train_station/near_station/static_mountaints,
	)

	/// What transition we want to set on leaving this station
	var/exit_transition
	/// How long it takes to end exit transition, -1 - means it stay until next transition change
	var/exit_transition_time = -1

	/// What transition we want to use before entering this station
	var/enter_transition
	/// How long before arrival at the station will set enter_transition
	var/enter_transition_time = 60 SECONDS

	/// Possible next stations. Empty by default and will be filled on load, but can be set in advance
	var/list/possible_next = list()
	// Whether this station blocks the train's movement, will be set automatically if the station has the TRAINSTATION_BLOCKING flag
	var/blocking_moving = FALSE

	VAR_FINAL/datum/looping_sound/global_sound/station_loop_sound = null
	VAR_FINAL/datum/map_template/template = null
	VAR_PRIVATE/list/docking_turfs = list()
	VAR_FINAL/datum/train_station/near_station/loaded_nearstation = null
	VAR_PRIVATE/unlock_password


/datum/train_station/New()
	. = ..()
	template = new /datum/map_template/train_station(map_path, "Train station - [name]", TRUE)
	template.returns_created_atoms = TRUE
	SSmapping.map_templates[template.name] = template

	if(ambience_sounds)
		create_ambience()

	map_object = new()
	map_object.name = name
	map_object.desc = desc
	map_object.associated_station = src
	map_object.set_position(0, 0)

/// TRUE if the map template parsed into a loadable size. Validates uploads.
/datum/train_station/proc/has_valid_template()
	return template && template.width > 0 && template.height > 0

/datum/train_station/proc/create_ambience()
	station_loop_sound = new(start_immediately = FALSE)
	station_loop_sound.create_from_list(ambience_sounds)

/// Admin: add an uploaded sound (file + loop length in ds) to the ambience and
/// rebuild the loop, restarting it if this station is currently loaded.
/datum/train_station/proc/admin_add_ambience(sound_file, duration_ds)
	if(!isfile(sound_file) || duration_ds <= 0)
		return FALSE
	if(!islist(ambience_sounds))
		ambience_sounds = list()
	ambience_sounds[sound_file] = duration_ds
	rebuild_ambience()
	return TRUE

/// Admin: remove the ambience entry at the given 1-based index.
/datum/train_station/proc/admin_remove_ambience(index)
	if(!islist(ambience_sounds) || index < 1 || index > length(ambience_sounds))
		return FALSE
	ambience_sounds -= ambience_sounds[index]
	rebuild_ambience()
	return TRUE

/datum/train_station/proc/rebuild_ambience()
	var/was_playing = (SStrain_controller.loaded_station == src)
	if(station_loop_sound)
		station_loop_sound.stop()
		QDEL_NULL(station_loop_sound)
	if(islist(ambience_sounds) && length(ambience_sounds))
		create_ambience()
		if(was_playing && station_loop_sound)
			station_loop_sound.start()

/datum/train_station/proc/connect_stations()
	for(var/i in 1 to length(possible_next))
		var/path = possible_next[i]
		var/datum/train_station/st = locate(path) in SStrain_controller.known_stations
		if(st)
			possible_next[i] = st
		else
			stack_trace("Invalid possible_next path [path] for station [type]")

	if(station_flags & TRAINSTATION_NO_NEARSTATION)
		return

	for(var/i in 1 to length(possible_nearstations))
		var/path = possible_nearstations[i]
		var/datum/train_station/st = locate(path) in SStrain_controller.known_stations
		if(st)
			possible_nearstations[i] = st
		else
			stack_trace("Invalid possible_nearstations path [path] for station [type]")

/datum/train_station/proc/get_spawnpoint()
	return locate(/obj/effect/landmark/trainstation/station_spawnpoint) in GLOB.landmarks_list

/datum/train_station/proc/get_spawn_offset(turf/spawn_turf)
	var/offset_x = spawn_turf.x
	var/offset_y = spawn_turf.y - template.height + 1
	var/offset_z = spawn_turf.z
	return list("x" = offset_x, "y" = offset_y, "z" = offset_z)


/datum/train_station/proc/load_station(datum/callback/load_callback, silent = FALSE)
	SHOULD_NOT_OVERRIDE(TRUE)

	if(!template)
		return FALSE
	var/start_time = world.realtime
	var/obj/effect/landmark/trainstation/spawnpoint = get_spawnpoint()
	if(!spawnpoint)
		stack_trace("Failed to load train station [name], no available spawnpoints!")
		return FALSE

	var/list/spawn_offset = get_spawn_offset(get_turf(spawnpoint))
	if(!islist(spawn_offset) || length(spawn_offset) != 3)
		stack_trace("Failed to load train station [name], invalid spawn offset!")
		return FALSE
	var/offset_x = spawn_offset["x"]
	var/offset_y = spawn_offset["y"]
	var/offset_z = spawn_offset["z"]

	var/turf/actual_spawnpoint = locate(offset_x, offset_y, offset_z)
	if(!actual_spawnpoint)
		stack_trace("Failed to load train station [name], template out of bounds")
		return FALSE
	var/bounds = template.load(actual_spawnpoint, centered = FALSE)

	docking_turfs = block(
		bounds[MAP_MINX], bounds[MAP_MINY], bounds[MAP_MINZ],
		bounds[MAP_MAXX], bounds[MAP_MAXY], bounds[MAP_MAXZ]
	)
	if(template.width < world.maxx)
		create_indestructible_borders(actual_spawnpoint)

	if((islist(possible_nearstations) && length(possible_nearstations)) && !(station_flags & TRAINSTATION_NO_NEARSTATION))
		var/datum/train_station/our_neatstation = pick(possible_nearstations)
		if(our_neatstation && our_neatstation.load_station(silent = TRUE))
			loaded_nearstation = our_neatstation

	var/load_in = world.realtime - start_time
	if(!silent)
		message_admins("TRAINSTATION: Loaded station [name] in [time2text(load_in, "ss")] seconds!")
	if(load_callback)
		load_callback.Invoke()
	after_load()
	SSdaylight?.handle_loaded_turfs(docking_turfs)
	return TRUE


/datum/train_station/proc/create_indestructible_borders(turf/bottom_left)
	SHOULD_NOT_OVERRIDE(TRUE)

	var/left_x = bottom_left.x - 1
	var/right_x = bottom_left.x + template.width
	var/start_y = bottom_left.y
	var/end_y = bottom_left.y + template.height - 1
	var/z = bottom_left.z

	for(var/y = start_y to end_y)
		var/turf/left_turf = locate(left_x, y, z)
		if(left_turf)
			left_turf.ChangeTurf(/turf/closed/indestructible/train_border)
			docking_turfs += left_turf


	for(var/y = start_y to end_y)
		var/turf/right_turf = locate(right_x, y, z)
		if(right_turf)
			right_turf.ChangeTurf(/turf/closed/indestructible/train_border)
			docking_turfs += right_turf
	if(right_x <= world.maxx)
		var/turf/corner = locate(right_x, end_y, z)
		if(!corner)
			return
		for(var/x = right_x to world.maxx)
			var/turf/top_turf = locate(x, end_y, z)
			if(top_turf)
				top_turf.ChangeTurf(/turf/closed/indestructible/train_border)
				docking_turfs += top_turf

/datum/train_station/proc/generate_password()
	var/static/list/possible_letters = \
		list("1", "2", "3",
			"4", "5", "6",
			"7", "8", "9", "0")
	var/new_pass = ""
	for(var/i = 1 to 5)
		new_pass += pick(possible_letters)
	return new_pass

/datum/train_station/proc/get_password()
	return unlock_password

/datum/train_station/proc/is_right_code(code)
	if(trim(code) != trim(unlock_password))
		return FALSE
	return TRUE

/datum/train_station/proc/pre_load()
	if(required_password)
		unlock_password = generate_password()

/datum/train_station/proc/after_load()
	if(station_flags & TRAINSTATION_BLOCKING)
		blocking_moving = TRUE
	if(station_loop_sound)
		station_loop_sound.start()

/datum/train_station/proc/setup_environment()
	if(environment_flags & ENVIRONMENT_UNDERGROUND)
		for(var/area/trainstation/TA in GLOB.areas)
			if(QDELETED(TA) || !TA.affected_by_environment)
				continue

			TA.daylight = FALSE
			TA.outdoors = FALSE
			TA.remove_daylight()
		var/datum/moving_turf_transition/undeground/TT = new()
		TT.process_instant()
		qdel(TT)

/datum/train_station/proc/clear_environment()
	if(environment_flags & ENVIRONMENT_UNDERGROUND)
		for(var/area/trainstation/TA in GLOB.areas)
			if(QDELETED(TA) || !TA.affected_by_environment)
				continue

			TA.daylight = initial(TA.daylight)
			if(TA.daylight)
				TA.initialize_daylight()
			TA.outdoors = initial(TA.outdoors)

/datum/train_station/proc/pre_unload()
	clear_environment()

/**
 * clear_turfs: pass FALSE when another station is being loaded straight over this one and its template covers at
 * least the same block. Contents are still purged; the turfs are left for the incoming template to overwrite,
 * which halves the ChangeTurf count of a swap - that was the single biggest cost in the load profile.
 */
/datum/train_station/proc/unload_station(datum/callback/unload_callback, clear_turfs = TRUE)
	SHOULD_NOT_OVERRIDE(TRUE)

	var/obj/effect/landmark/trainstation/crew_spawnpoint/crew_mover = locate() in GLOB.landmarks_list

	Master.StartLoadingMap()

	for(var/turf/T in docking_turfs)
		for(var/atom/movable/AM in T.contents)
			if(HAS_TRAIT(AM, TRAIT_NO_STATION_UNLOAD))
				continue
			if(isliving(AM))
				var/mob/living/living = AM
				if(crew_mover && living.client)
					living.forceMove(get_turf(crew_mover))
					to_chat(living, span_warning("You barely made it to the train before it departed!"))
					continue
			if(isobserver(AM))
				continue
			POOL_ASYNC_RELEASE(AM)
		// Still reset baseturfs either way: the incoming ChangeTurf inherits them, so skipping this would leave
		// the outgoing station's base under the new one.
		if(!clear_turfs)
			T.baseturfs = /turf/open/space
			continue
		T.ChangeTurf(/turf/open/space, null, CHANGETURF_DEFER_CHANGE)
		T.baseturfs = /turf/open/space

	docking_turfs.Cut()
	template.created_atoms = null
	if(unload_callback)
		unload_callback.Invoke()
	Master.StopLoadingMap()
	if(loaded_nearstation)
		loaded_nearstation.unload_station()
		loaded_nearstation = null
	after_unload()

/datum/train_station/proc/after_unload()
	if(station_loop_sound)
		station_loop_sound.stop()


/// Generic station built at runtime from an admin-supplied map (uploaded file
/// or an existing template's path). Not auto-loaded; constructed by
/// SStrain_controller.admin_create_station() with a map + name.
/datum/train_station/custom
	name = "Custom Station"
	map_path = null
	creator = "Admin"

/datum/train_station/custom/New(map_file, new_name)
	if(new_name)
		name = new_name
	if(map_file)
		map_path = map_file
	. = ..()


/datum/train_station/near_station
	name = "Near station"
	station_flags = TRAINSTATION_ABSCTRACT | TRAINSTATION_NO_FORKS | TRAINSTATION_NO_SELECTION | TRAINSTATION_NO_NEARSTATION
	possible_nearstations = null
	visible = FALSE

/datum/train_station/near_station/get_spawnpoint()
	return locate(/obj/effect/landmark/trainstation/nearstation_spawnpoint) in GLOB.landmarks_list

/datum/train_station/near_station/get_spawn_offset(turf/spawn_turf)
	return list("x" = spawn_turf.x, "y" = spawn_turf.y, "z" = spawn_turf.z)
